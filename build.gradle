plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "com.github.node-gradle.node" version "2.2.3"
    id 'org.jetbrains.kotlin.jvm' version '1.6.20'
    id "org.jetbrains.kotlin.plugin.spring" version "1.6.20"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.6.20"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.6.20"
    id "org.jetbrains.kotlin.kapt" version "1.6.20"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allOpen {
    annotation 'javax.persistence.Entity'
}

kapt {
    keepJavacAnnotationProcessors = true
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

node { 
    version = '12.16.1' 
    npmVersion = '6.13.4' 
    yarnVersion = "1.22.4"
    npmInstallCommand = 'install' 
    distBaseUrl = 'https://nodejs.org/dist' 
    download = true 
    workDir = file("${project.buildDir}/front") 
    nodeModulesDir = file("${project.projectDir}/front") 
    workDir = file("${project.projectDir}/.gradle/nodejs")
    yarnWorkDir = file("${project.projectDir}/.gradle/yarn")
} 

ext.profile = (!project.hasProperty('profile') || !profile) ? 'local' : profile

bootRun {
  args = ["--spring.profiles.active=" + profile]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.auth0:java-jwt:3.18.2'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation "com.querydsl:querydsl-jpa:5.0.0"
    implementation "com.querydsl:querydsl-core:5.0.0"
    implementation 'org.jetbrains:annotations:20.1.0'
    compileOnly 'org.springframework.boot:spring-boot-starter-jdbc'
    runtimeOnly 'mysql:mysql-connector-java'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.h2database:h2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    kapt "com.querydsl:querydsl-apt:5.0.0:jpa"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.6.20"
}

task deleteVueArtifact(type: Delete) {
    delete "src/main/resources/static"
}

task yarnBuild(type: YarnTask, dependsOn: ['deleteVueArtifact', 'yarn_install']) { 
    args = ["run", "build" + profile] 
}

processResources.dependsOn 'yarnBuild'

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
