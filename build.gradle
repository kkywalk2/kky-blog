plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id "com.github.node-gradle.node" version "2.2.3"
    id 'org.jetbrains.kotlin.jvm' version '1.9.21'
    id "org.jetbrains.kotlin.plugin.spring" version "1.9.21"
    id "org.jetbrains.kotlin.kapt" version "1.9.21"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

kapt {
    keepJavacAnnotationProcessors = true
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

node { 
    version = '20.11.0'
    npmVersion = '8.19.2'
    yarnVersion = '1.22.4'
    npmInstallCommand = 'install'
    distBaseUrl = 'https://nodejs.org/dist' 
    download = true 
    workDir = file("${project.buildDir}/front-react-vite")
    nodeModulesDir = file("${project.projectDir}/front-react-vite")
    workDir = file("${project.projectDir}/.gradle/nodejs")
    yarnWorkDir = file("${project.projectDir}/.gradle/yarn")
} 

ext.profile = (!project.hasProperty('profile') || !profile) ? 'local' : profile

bootRun {
  args = ["--spring.profiles.active=" + profile]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.auth0:java-jwt:3.18.2'
    implementation 'org.jetbrains:annotations:20.1.0'

    implementation 'org.jetbrains.exposed:exposed-java-time:0.45.0'
    implementation 'org.jetbrains.exposed:exposed-spring-boot-starter:0.45.0'

    runtimeOnly 'mysql:mysql-connector-java:8.0.32'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.h2database:h2:2.2.224'
}

task deleteVueArtifact(type: Delete) {
    delete "src/main/resources/static"
}

task npmBuild(type: NpmTask, dependsOn: ['deleteVueArtifact', 'npm_install']) {
    args = ["run", profile == 'prod' ? 'build' : 'devbuild']
}

task copyReactBuildFiles1(type: Copy, dependsOn: ['npmBuild']) {
    from "front-react-vite/dist"
    into "src/main/resources/static"
}

task copyReactBuildFiles2(type: Copy, dependsOn: ['copyReactBuildFiles1']) {
    from "front-react-vite/assets"
    into "src/main/resources/static/assets"
}

compileJava.dependsOn(copyReactBuildFiles2)

tasks.named('processResources') {
    dependsOn copyReactBuildFiles1, copyReactBuildFiles2
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '21'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '21'
    }
}
